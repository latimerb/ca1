// Replace all USER_DEFINED_SECTIONs

begintemplate pyrtypea //BEGIN_USER_DEFINED_SECTION templatename END_USER_DEFINED_SECTION
public init, connect_sections, size_sections, append_sections
public mechinit, insert_mechs, set_biophys, get_root
public  pre_list, connect_pre, is_art, is_connected, gid, randi
public soma, apical, basal, axon
public all, basal_list, apical_list, soma_list, axon_list, dendrite_list
public x, y, z, position, myroot, Vrest
public NumSoma, NumApical, NumBasal, NumAxon

// strings
strdef myroot

// objects
objref syn, pre_list, templist, rootlist, this

// external variables
external numCellTypes, cellType

// create the sections[segments]
//BEGIN_USER_DEFINED_SECTION
NumSoma=1
NumApical=1
NumBasal=1
NumAxon=11

create soma[NumSoma], apical[NumApical], basal[NumBasal], axon[NumAxon]

//END_USER_DEFINED_SECTION
	
proc init() {
	gid = $1
	randi = $2
	
	// morphology
	connect_sections()	// local fcn: connect soma, dendrites, axon initial segment
	size_sections()		// local fcn: set the size dimensions of each section
	define_shape()		// builtin fcn: fill in 3d info for sections defined by only L and diam, translate 3d points for consistency with their connections 
  	append_sections()	// local fcn: append all sections to the section list
	set_nseg()			// local fcn: set the number of segments in each section
	get_root()			// local fcn: perform morphology checks

	// electrophysiology
	mechinit()			// local fcn: set values for max conductances and reversal potentials of ion channels and other ephys parameters that are subject to fitting
	insert_mechs()		// local fcn: insert ion channels and actually set values determined in the mechinit fcn
	set_chanparams()	// local fcn: after all channels have been inserted, then their other parameters can be set	

	// synapses
	pre_list = new List() // define a list for the presynaptic connections
	define_synapses($3)	// local fcn: define all possible synaptic connections received by this cell
}

proc connect_sections() { local i

	//Connect the dends to somas, etc
	//Example: 
	// connect dend[0](0), soma(1)
	// for i=0,3 {
	// 	 connect dend[i+1](0), dend[i](1)
	// }
	//BEGIN_USER_DEFINED_SECTION
	connect apical[0](0), soma[0](0)

    connect basal[0](0), soma[0](0)
		
	//axon[0:9] are the AIS, axon[10] is the axon
	connect axon[0](0), soma[0](1)
		
	for i = 0,9{ connect axon[i+1](0) , axon[i](1)}	
	
	
	//END_USER_DEFINED_SECTIONS
	
}

objref all, basal_list, apical_list, dendrite_list, soma_list, axon_list

proc append_sections() { local i
	objref all, basal_list, apical_list, dendrite_list, soma_list, axon_list

	all = new SectionList()
	basal_list = new SectionList()
	apical_list = new SectionList()
	soma_list = new SectionList()
	axon_list = new SectionList()
	dendrite_list = new SectionList()
	
	//Add sections to the list accessed by mother program
	//Example:
	// soma all.append()
	// soma soma_list.append()
	// for i=0,15 {
	//	 dend[i] all.append()
	//	 dend[i] dendrite_list.append()
	// }
	//BEGIN_USER_DEFINED_SECTION
	
	soma[0] all.append()
	soma[0] soma_list.append()
	
	apical[0] all.append()
	apical[0] apical_list.append()
	
	basal[0] all.append()
	basal[0] basal_list.append()
	
	for i=0, NumAxon-1 axon[i] { all.append()
								 axon_list.append()}
	
	//END_USER_DEFINED_SECTION
}

proc size_sections() { local i
	//BEGIN_USER_DEFINED_SECTION
	soma[0] {L = 25 diam = 24.75 nseg = 1}
	apical[0] {diam = 3 L=270 nseg=8}     		// 3.5
	basal[0] {diam =5 L=555 nseg=7}
	for i=0,9 axon[i] { L = 5 nseg = 10 diam = 1.22}
	axon[0]	{diam(0:1) = 1.7:1.5}											
	axon[1]	{diam(0:1) = 1.5:1.22}
	axon[10] {L = 100 nseg = 1 diam = 1}
	//END_USER_DEFINED_SECTION
}

external lambda_f
proc set_nseg() {
  	forsec all { nseg = int((L/(0.1*lambda_f(100))+.9)/2)*2 + 1  }
}

proc mechinit() {
	// Temperature of simulation
	celsius = 31.0
	
	//Initialize properties of channels, vrest
	//Example:
	// CmSoma= 1.4
	// CmDend = 1.4
	// ca_outside = 2
	// ca_inside = 5.e-6 // 50.e-6
	// catau = 10
	// gNav     = 0.15
	//BEGIN_USER_DEFINED_SECTION
	Rm = 80000
	Cm = 2.4
	
	RaAll = 150
	RaSoma = 150
	RaDend = 150
	RaAx = 150
	Vpas = -72
	
	gna = 0.015 
	gnap = 0.000559 
	gkdr = 0.002
	ghd = 1.5e-5
	gca = 5.5e-4 
	gm = 0.00224 
	gsAHP = 0.05
	gkap = 0.002 
	gleak = 2.5e-5
	gleak_dend=1.57*3.0e-5
	gna12_ais_max = 3200   // Nav1.2 
	gna16_ais_max = 3200   // Nav1.6
	gkv_axon = 1000
	nash=10
	Vrest = -70
	 	
	//END_USER_DEFINED_SECTION
	
}

proc insert_mechs() {
	//*** insert the channels into the cells
	//Example:
	// forsec all {		
	//	insert ch_KvA
	//	gmax_ch_KvA = gKvA
	// }
	// soma {
	//  insert ch_Nav	
	//  gmax_ch_Nav = gNav
	// }
	// forsec dendrite_list {
	// }
	//BEGIN_USER_DEFINED_SECTION
	
	soma {
		insert cadyn gcabar_cadyn = gca  eca = 120
		insert leak el_leak= -72  glbar_leak = 1.0*gleak   Ra=RaSoma cm = Cm // 2.5
		insert hd ghdbar_hd = ghd ehd_hd=-45
		insert na3 sh_na3=nash ar_na3=1	gbar_na3 = 3*gna
		insert nap gbar_nap = gnap
		insert kdr gbar_kdr = gkdr
		insert capool taucas = 1000  fcas_capool = 0.05 //only in dend and p_dend
		insert sAHP gsAHPbar_sAHP = gsAHP
		insert im gbar_im = gm
		insert kap gkabar_kap = gkap
		
		v=Vrest
		ena = 45
		ek = -80
	}
	apical[0] {
		insert cadyn gcabar_cadyn = gca eca = 120
		insert leak el_leak = -72  glbar_leak = gleak_dend Ra=RaDend cm = Cm // 2.5
		insert hd ghdbar_hd = ghd ehd_hd=-45
		insert na3 sh_na3=nash ar_na3=1	gbar_na3 = gna
		insert nap gbar_nap = 0  gbar_nap(1/(2*nseg)) = 0.8*gnap
		insert kdr gbar_kdr = gkdr
		insert capool taucas = 1000  fcas_capool = 0.05 
		insert sAHP gsAHPbar_sAHP = 0
		insert im gbar_im = 0 gbar_im(1/(2*nseg)) = 0.8*gm
		insert kap gkabar_kap = 0
				
		v=Vrest
		ena = 45
		k = -80
	}
	basal[0] {
		insert cadyn gcabar_cadyn = gca eca = 120
		insert leak el_leak = -72  glbar_leak = gleak_dend Ra=RaDend cm = Cm // 2.5
		insert hd ghdbar_hd = ghd ehd_hd=-45
		insert na3 sh_na3=nash ar_na3=1	gbar_na3 = gna
		insert nap gbar_nap = 0  gbar_nap(1/(2*nseg)) = 0.8*gnap
		insert kdr gbar_kdr = gkdr
		insert capool taucas = 1000  fcas_capool = 0.05 
		insert sAHP gsAHPbar_sAHP = 0
		insert im gbar_im = 0 gbar_im(1/(2*nseg)) = 0.8*gm
		insert kap gkabar_kap = 0
				
		v=Vrest
		ena = 45
		k = -80
	}
	for i=0,9 axon[i] {
		insert pas
		Ra = 50000//150
		cm = 0.5
		g_pas = 1/30000
		e_pas = -70
		insert na12  gbar_na12=0
		insert na16  gbar_na16=0
		insert kv    gbar_kv = 0
		ena = 45
		ek = -80
	}
	axon[0]. gbar_na12= gna12_ais_max      *	0.96	*	1                  								
	axon[1]. gbar_na12= gna12_ais_max  		*	0.9	*	1                    
	axon[2]. gbar_na12= gna12_ais_max  		*	0.75	*	1                  
	axon[3]. gbar_na12= gna12_ais_max  		*	0.55	*	0.95               
	axon[4]. gbar_na12= gna12_ais_max  		*	0.366985879	*	0.880142857  
	axon[5]. gbar_na12= gna12_ais_max  		*	0.2	*	0.75                 
	axon[6]. gbar_na12= gna12_ais_max  		*	0.100330761	*	0.647857143  
	axon[7]. gbar_na12= gna12_ais_max  		*	0.011532125	*	0.520285714  
	axon[8]. gbar_na12= gna12_ais_max  		*	0	*	0.428571429            
	axon[9]. gbar_na12= gna12_ais_max  		*	0	*	0.342857143 
		
	axon[0]. gbar_na16 = gna16_ais_max	*	0.04	*	1   
	axon[1]. gbar_na16 = gna16_ais_max	*	0.1	*	1   
	axon[2]. gbar_na16 = gna16_ais_max	*	0.25	*	1   
	axon[3]. gbar_na16 = gna16_ais_max	*	0.45	*	0.95   
	axon[4]. gbar_na16 = gna16_ais_max	*	0.633014121	*	0.880142857   
	axon[5]. gbar_na16 = gna16_ais_max	*	0.8	*	0.75   
	axon[6]. gbar_na16 = gna16_ais_max	*	0.899669239	*	0.647857143   
	axon[7]. gbar_na16 = gna16_ais_max	*	0.988467875	*	0.520285714   
	axon[8]. gbar_na16 = gna16_ais_max	*	1	*	0.428571429   
	axon[9]. gbar_na16 = gna16_ais_max	*	1	*	0.342857143  
				
	axon[0]. gbar_kv = gkv_axon	*	0.2 
	axon[1]. gbar_kv = gkv_axon	*	0.3 
	axon[2]. gbar_kv = gkv_axon	*	0.4 
	axon[3]. gbar_kv = gkv_axon	*	0.5 
	axon[4]. gbar_kv = gkv_axon	*	0.6 
	axon[5]. gbar_kv = gkv_axon	*	0.7 
	axon[6]. gbar_kv = gkv_axon	*	0.8 
	axon[7]. gbar_kv = gkv_axon	*	0.9
	axon[8]. gbar_kv = gkv_axon	*	1
	axon[9]. gbar_kv = gkv_axon	*	1
	
	axon[10]{
		insert leak el_leak= -72  glbar_leak = 1.0*gleak 
		Ra = 250//150
		cm = 2
		insert na12  gbar_na12=0
		insert na16  gbar_na16=gna16_ais_max	*	1	*	0.342857143  //1300
		insert kv    gbar_kv= gkv_axon//200
		ena = 45
		ek = -80
	}
	//END_USER_DEFINED_SECTION

}


proc set_chanparams() {
	//*** insert the channels into the cells
	//Example:
	// forsec all {
	//  ena = enaval
	//  ek = ekval
	//  eca = ecaval
	//  e_ch_leak = eleakval
	//  cao_iconc_Ca = ca_outside
	// }
	//BEGIN_USER_DEFINED_SECTION
	
	
	
	//END_USER_DEFINED_SECTION

}

proc connect_pre() {  // $o1 target point process, $o2 returned NetCon
	soma $o2 = new NetCon (&v(1), $o1)
	$o2.threshold = -10
}

func is_art()  { //Artificial Cell?
	return 0 
}

proc position(){ local i
	forall {
		for i = 0, n3d()-1 {
			pt3dchange(i, $1-x+x3d(i), $2-y+y3d(i), $3-z+z3d(i), diam3d(i))
		}
	}
	x = $1  y = $2  z = $3	
}

proc get_root() {local i localobj sref
	rootlist = new SectionList()
	rootlist.allroots()
	i=0
	forsec all {
		sref = new SectionRef()
		if (sref.has_parent==0) {
			myroot = secname()
			i=i+1
		}
		for(x,0) {
			if (diam(x) <=0.01) print "WARNING: tiny diameter of ",  diam(x), " um at ", secname(), ", point ", x, "!"
			if (diam3d(x) <=0.01) print "WARNING: tiny 3d diameter of ", diam3d(x), " um at ", secname(), ", point ", x, "!"
		}
		if (L <=0.001) print "WARNING: tiny length of ", L, " um at ", secname(), "!"
	}
	if (i>1) {
		print "WARNING: cell ", gid, " has ", i, " root sections!"
	}
}

strdef myStr

objref newSecRef, syn
proc define_synapses() {
	ind = $1
	i = 0

	access soma[0]
	{distance()}

	for celltype = 0, numCellTypes-1 {
		templist = new List ()
		for r=0, cellType[ind].SynList[celltype].count()-1 {
			execute(cellType[ind].SynList[celltype].object(r).NewSynStr, this) // sets newSecRef
						
			forsec newSecRef {		
				for (x,0) {
					execute(cellType[ind].SynList[celltype].object(r).CondStr, this)
					 if (y==1) {
						execute(cellType[ind].SynList[celltype].object(r).SynStr, this)
						if(cellType[ind].SynList[celltype].object(r).CustomFlag==1) {
							if (strcmp(cellType[ind].SynList[celltype].object(r).cust1,"")!=0) {
									execute(cellType[ind].SynList[celltype].object(r).cust1, this)
							}
							if (strcmp(cellType[ind].SynList[celltype].object(r).cust2,"")!=0) {
									execute(cellType[ind].SynList[celltype].object(r).cust2, this)
							}
							if (strcmp(cellType[ind].SynList[celltype].object(r).cust3,"")!=0) {
									execute(cellType[ind].SynList[celltype].object(r).cust3, this)
							}
							if (strcmp(cellType[ind].SynList[celltype].object(r).cust4,"")!=0) {
									execute(cellType[ind].SynList[celltype].object(r).cust4, this)
							}
							if (strcmp(cellType[ind].SynList[celltype].object(r).cust5,"")!=0) {
									execute(cellType[ind].SynList[celltype].object(r).cust5, this)
							}
						} else {
							if (cellType[ind].SynList[celltype].object(r).GABAabFlag==0) {
								syn.tau1 = cellType[ind].SynList[celltype].object(r).tau1
								syn.tau2 = cellType[ind].SynList[celltype].object(r).tau2
								syn.e = cellType[ind].SynList[celltype].object(r).efirst
								if (strcmp(cellType[ind].SynList[celltype].object(r).SynType,"MyExp2Sidnw")==0) {
									execute(cellType[ind].SynList[celltype].object(r).Scaling, this)
								}
							} else {
								syn.tau1a = cellType[ind].SynList[celltype].object(r).tau1a
								syn.tau2a = cellType[ind].SynList[celltype].object(r).tau2a
								syn.ea = cellType[ind].SynList[celltype].object(r).ea
								syn.tau1b = cellType[ind].SynList[celltype].object(r).tau1b
								syn.tau2b = cellType[ind].SynList[celltype].object(r).tau2b
								syn.eb = cellType[ind].SynList[celltype].object(r).eb
							}
						}
						syn.sid = i
						templist.append(syn)
						i = i + 1
					}
				}
			}
		}
		pre_list.append(templist)
		findme = 1
	}
}
endtemplate pyrtypea //BEGIN_USER_DEFINED_SECTION templatename END_USER_DEFINED_SECTION